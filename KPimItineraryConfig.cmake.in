# SPDX-FileCopyrightText: 2018-2019 Laurent Montel <montel@kde.org>
# SPDX-FileCopyrightText: 2018-2019 Volker Krause <vkrause@kde.org>
# SPDX-License-Identifier: BSD-3-Clause

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(Qt@QT_MAJOR_VERSION@Gui "@QT_REQUIRED_VERSION@")
find_dependency(KPim@KF_MAJOR_VERSION@Mime "@KMIME_VERSION@")
# Compat
if ("@KF_MAJOR_VERSION@" EQUAL "5")
    find_dependency(KF5Mime "@KMIME_VERSION@")
endif()
find_dependency(KF@KF_MAJOR_VERSION@CalendarCore "@KF_MIN_VERSION@")
find_dependency(KF@KF_MAJOR_VERSION@Contacts "@KF_MIN_VERSION@")
find_dependency(KPimPkPass "@PIM_PKPASS@")

if (NOT @BUILD_SHARED_LIBS@)
    find_dependency(ZLIB)
    find_dependency(Poppler)
    find_dependency(ZXing)
    if (@HAVE_LIBXML2@)
        find_dependency(LibXml2)
    endif()
endif()

if (@KITINERARY_KF5_COMPAT@)
    message(WARNING "\n"
                    "************* DEPRECATION WARNING *************\n"
		    "Using find_package(KPimItinerary) and KPim::Itinerary targets is deprecated. "
                    "Please use find_package(KPim@KF_MAJOR_VERSION@Itinerary) and KPim@KF_MAJOR_VERSION@::Itinerary instead. This backwards "
                    "compatibility will be removed in the next major version.\n"
                    "***********************************************\n")
endif()


include("${CMAKE_CURRENT_LIST_DIR}/KPim@KF_MAJOR_VERSION@ItineraryTargets.cmake")
@PACKAGE_INCLUDE_QCHTARGETS@
